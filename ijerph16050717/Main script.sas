/*
Stomach Cancer and Exposure to Non-asbestiform Talc Powder via Chinese Herbal Medicine: A Population-based Cohort Study
Time: January 1st 1997 ~ December 31st 2013
Patients: LHID 2005
Intervention: talc inake from Chinese herbal medicine
Comparison: patients in LHID 2005 without talc intake
Outcome: Diagnosis of stomach cancer
*/

/*OPTIONS settings*/
options nolabel nonumber nodate mprint;    /*  
                                                                         NOLABEL ---  not using labels with variables
                                                                         NONUMBER --- not printing the page number
                                                                         NODATE --- the date and the time are not printed
                                                                         MPRINT --- displays the SAS statements that are generated by macro execution */

/*Data library settings*/
libname talc "\talc\";     /*directory for data output */
libname M "\main datasets\";            /*directory for data input from LHID 2005 */
libname drug "\supplement datasets\";   /*directory for data regarding drug codes and urbanization levels*/

/*Setting directory of SAS modules*/
%let basepath = \SAS modules\;


/* Constructing the "ID List" of target population*/
%include "&basepath.id list first id_20180818.sas";

/* Data exclusion:
The exclusion criteria were: 
	- patients younger than 20, 
	- patients with diagnosis of cancer in 1997, 
	- patients with diagnosis of H.pylori / Peptic ulcer disease / Duodenal ulcer / Gastric ulcer / Gastritis / Duodenitis, 
Parameters:
	lib_from: library containing HV files (Registry for catastrophic illness patients)
	cancer_icd: icd-9-cm code of the cancer of interest (stomach cancer in our case)
	ID_list: target file containing the "ID list" to be marked for cancer diagnosis
*/
%include "&basepath.exclusion_cancer_mark.sas";
%exclusion_cancer_mark(m, '151', talc.ID_list); /*the ICD-9-CM code of stomach cancer is "151" */
%include "&basepath.exclusion_hpylori.sas";
%exclusion_hpylori(talc.ID_list); /*Excluding patients with H.pylori / Peptic ulcer disease / Duodenal ulcer / Gastric ulcer / Gastritis / Duodenitis */


/*Import the drug number of talc-containing medication*/ 
DATA drug.talc_chinese; SET drug.talc_chinese; RUN;

/*Finding the medical talc prescription data*/
%include "&basepath.drug_containing_prescription.sas";
/* using the drug code to identify all ambulatory care orders containing the target medication,
and store the results in the dataset "ooCE"*/
	/* Parameters
	(OO = the NHIRD dataset containing the details of ambulatory care orders)
	lib_from: the library where OO files exist.	
	file_drug: the file with required drug number
	lib_out: the library to output the target file ooCE
	year_from: starting year of OO files 
	year_to: ending year of OO files
	*/
%drug_containing_prescription_1(m, drug.talc_chinese, talc, 1997, 2013);
/*Using the "ooCE" dataset (ambulatory care orders containing target medication) to identify corresponding CD files,
in order to find out the ID and the diagnoses*/
	/*Parameters
	(CD = the NHIRD dataset containing the ambulatory care expenditures by visits)
	lib_from: the library where CD files exist.	
	lib_out: the library to output the target file cdooCE
	year_from: starting year of CD files 
	year_to: ending year of CD files
	*/
%drug_containing_prescription_2(m, talc, 1997, 2013);

/*Pooled together all medical talc prescription data, and sort by the ID*/
Data talc.exposure_data;		set talc.cdooCE1997-talc.cdooCE2013;	
proc sort;	by ID;	run;

/*View the main diagnoses corresponding to the medical talc prescription data*/
PROC SORT Data = talc.exposure_data;		/*sort the medical talc prescription data by drugs and then by ID*/
BY talc_frc id; RUN;
DATA talc.icd9main_exposure; SET talc.exposure_data; /*icd9main_exposure: containing the main diagnosis (dx_exposure) of each prescription*/
dx_exposure = substr(icd9_1, 1, 3); RUN;
PROC FREQ; /*icd9_by_drug: output table of the main diagnosis of each prescription by drugs*/
BY talc_frc;
TABLES dx_exposure / norow nopercent out = talc.icd9_by_drug;
RUN;
PROC SORT Data = talc.icd9_by_drug;	 /*to identify the most frequent diagnosis of talc-containing prescription*/
BY talc_frc descending count; RUN;

/*Pre-process of analyzing the demographics (Table 1)*/
/*adding variable "ever_talc": whether the patient ever exposed to talc*/
PROC SQL; CREATE TABLE talc.ID_list as
SELECT *, case
	when id in (select id from talc.exposure_data) then 1
	else 0
	end as ever_talc
FROM talc.ID_list; QUIT;


/*Table 1.demographics: talc-exposed vs. unexposed
	age (at the start date of the study)
	sex (gender of the beneficiary)
	income (monthly income in TWD)
	fu_time: follow-up time
	urban: level of urbanization (high, medium, low)
*/
	/* Paremeters:
		ID_list: target file containing the ID list
		depend: dependent variable for grouping
	*/
%include "&basepath.demographic_analysis.sas";
%demographic_analysis(talc.ID_list, ever_talc);


/*Checking the cross table of "ever exposed to talc" and "diagnosis of stomach cancer"*/
proc freq data = talc.id_list;
tables ca * ever_talc  / norow nopercent chisq;	run;


/*statistics of the follow-up time; summing up the follow-up person-year*/
PROC MEANS data = talc.id_list N MIN MEAN STD MAX Q1 MEDIAN Q3 SUM;
var fu_time; run;


/*=============================================*/
/*Calculate the Charlson Comorbidity Index (CCI)*/
/* Calculate the CCI based on icd-9-cm codes (and three visits with the same diagnosis in one year)*/
	     /*  dbname: name of the library containing records of OPD visits
	          outfile: the target file for marking the diagnoses*/
%include "&basepath.CCI calculation.sas";
%dx2times(talc.ID_list);
Proc Freq data = talc.ID_list; tables Charlson * ever_talc; run; /*Check the relationship between Chalson score (CCI) and exposure ot talc*/
Data talc.ID_list; Set talc.ID_list; /*Define Charlson > 2 as high CCI*/
	if not (Charlson > 0) then Charlson = 0; /* missing value --> 0*/
	if Charlson <= 2 then CCI = "low "; 		else CCI = "high";
run;
/*Check the relationship between levels of Chalson score (high CCI and low CCI) and exposure ot talc*/
proc freq; tables CCI * ever_talc  / norow nopercent chisq;	run;


/* Assessing the amount of talc exposure and dealing with the missing value*/
	/*Parameter:
		drug_data: dataset with drug prescription data
	*/
%include "&basepath.chinese_herb_process.sas";
%chinese_herb_process(talc.exposure_data);


/*Before the "time-dependent process", sum up the exposure and compare between cancer vs. non-cancer*/
	/*Parameters
		ID_list: target file containing the ID list
		drug_data: dataset with drug prescription data
		output_file: the name of the resulting output file
	*/
%include "&basepath.summing_exposure.sas";
%summing_exposure(talc.ID_list, talc.exposure_data, ID_list);

/*Check median and quartile for talc exposure*/
PROC MEANS data = ID_LIST RANGE Q1 MEDIAN Q3 MEAN SUM;
var sum_dose; RUN; /*the results: Q1 = 6, Median = 10.5, Q3 = 21*/

/*(before the "time-dependent process") grouping of the talc exposure into 3 levels*/
	/* Parameters:
		target_file: the target dataset for Cox regression 
		scalar: scalar variable to be categorized by the cut-off values
		cutoff01: cut-off point 01
		cutoff02: cut-off point 02
		level: categorical variable resulting from the cut-offs
		depend: depending variable
	*/
%include "&basepath.level_of_dose.sas"; 
%level_of_dose(ID_list, sum_dose, 6, 21, level_dose, ca); /*(levels of talc exposure) low: <6, median: 6~21, high: >21*/

/*"time-dependent process": We treated the talc exposure as a time-dependent variable 
in order to eliminate the immortal time bias, which is a form of selection bias arising 
when the period between cohort entry and date of first exposure to a drug is either misclassified 
or simply excluded because the event of interest has not occurred*/
	/*Parameter
		drug_data: dataset with drug prescription data
	*/
%include "&basepath.time_dependent_exposure.sas";
%time_dependent_exposure(talc.exposure_data); /*For patients ever received medical prescription of talc, 
we considered the time interval between the beginning of the study (January 1st 1997) and the date of 
first prescription of talc to be a non-exposure period, whereas the time interval from the date of first prescription 
of talc to the endpoint of follow-up was recognized as an exposure period. */

/*Procedures before doing the regression*/
	/*Mapping the exposure data to the target cohort (the "id_list")*/
	/*For each time period: set up the event status and the time-to-event*/
	/*For people without talc exposure, set up a non-exposure period from Jan/01/1997 to follow-up end date (fu_date)*/
	/*ever_talc (whether she had ever taken talc) needs to be rechecked*/
	/*fu_time (follow-up time) needs to be re-calculated*/
	/*if the time period is not the last for that person, set the follow-up endpoint (fu_date) to the end date of that period (drug_end_date)*/
	/*defining "elder" as age >= 65*/
	/* Parameters:
	ID_list: target file containing the ID list
	drug_data: dataset with drug prescription data
	output_file: the name of the resulting output file for subsequent regression 
	*/
%include "&basepath.before_regression_process.sas";
%before_regression_process(talc.ID_list, talc.exposure_data, talc.before_regression);



/*(after the "time-dependent process") grouping of the talc exposure into 3 levels*/
	/* Parameters:
		target_file: the target dataset for Cox regression 
		scalar: scalar variable to be categorized by the cut-off values
		cutoff01: cut-off point 01
		cutoff02: cut-off point 02
		level: categorical variable resulting from the cut-offs
		depend: depending variable
	*/
%include "&basepath.level_of_dose.sas";
%level_of_dose(talc.before_regression, sum_dose, 6, 21, level_dose, ca);


/*Reporting numbers of outcome events or summary measures over time.*/
	/*Parameters
		ID_list: target file containing the ID list to calculate the of outcome events
		event: the name of variable of interest (the event)
		fu_time_year: the variable name containing the follow-up time (year)
	*/
%include "&basepath.num_of_event_table.sas";
%num_of_event_table(talc.before_regression);


/*2018.01 Cox regression of talc
dependent variable: diagnosis of stomach cancer (ca)
explanatory variables: ever exposure to talc (ever_talc), level of talc exposure (level_dose)
confounders: age, gender, CCI*/
	/* Parameter:
		ID_list: target file containing the time, the outcome, and the variables.
	*/
%include "&basepath.Cox_regression_of_talc.sas";
%Cox_regression_of_talc(talc.before_regression);



/*Sensitivity analysis: Cut-off points of talc exposure*/
	/*alternative way of grouping of the talc exposure into 3 levels (unexposed, ? median, > median)*/
	Data talc.before_regression_S1; Set talc.before_regression; Run;
	%include "&basepath.level_of_dose.sas";
	%level_of_dose(talc.before_regression_S1, sum_dose, 0, 10.5, level_dose, ca);
	/*Report numbers of outcome events or summary measures over time.	*/
	%include "&basepath.num_of_event_table.sas";
	%num_of_event_table(talc.before_regression_S1);
	/*Cox regression of talc*/
	%include "&basepath.Cox_regression_of_talc.sas";
	%Cox_regression_of_talc(talc.before_regression_S1);


/*Sensitivity analysis: Minimal induction period*/
	Data talc.before_regression_S2; Set talc.before_regression; 
	if time < 5 then delete;		Run;  /*delete all follow-up time < 5 years, to make sure minimal induction period to be at least 5 years*/
	%include "&basepath.num_of_event_table.sas";
	%num_of_event_table(talc.before_regression_S2);
	/*Cox regression of talc*/
	%include "&basepath.Cox_regression_of_talc.sas";
	%Cox_regression_of_talc(talc.before_regression_S2);

/*Sensitivity analysis: CCI*/
	/*Redefine CCI>0 as high comorbidities*/
	Data talc.before_regression_S3; Set talc.before_regression; 
	if Charlson > 0 then CCI = "high"; 		else CCI = "low ";
	Run;
	%include "&basepath.num_of_event_table.sas";
	%num_of_event_table(talc.before_regression_S3);
	/*Cox regression of talc*/
	%include "&basepath.Cox_regression_of_talc.sas";
	%Cox_regression_of_talc(talc.before_regression_S3);
	Data talc.ID_list_S3; Set talc.ID_list; 
	if Charlson > 0 then CCI = "high"; 		else CCI = "low ";
	run;
	/*Checking the relationship between level of CCI and ever exposure to talc*/
	proc freq; tables CCI * ever_talc  / norow nopercent chisq;	run;
